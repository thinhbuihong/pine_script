//@version=5
indicator("QQE v5", shorttitle="QQE v5")

src = close
Fast = input.float(2.618, title="Fast Factor")
Slow = input.float(4.236, title="Slow Factor")
RSI = input.int(14, title="RSI Length")
SF = input.int(2, title="Slow Factor")

// Wilder's Moving Average (WiMA)
WiMA(src, length) =>
    var float MA_s = na
    MA_s := (src + nz(MA_s[1]) * (length - 1)) / length

RSIndex = ta.ema(ta.rsi(close, RSI), SF)

// Smoothed ATR of Smoothed RSI
TR = math.abs(RSIndex - RSIndex[1])

AtrRsi = WiMA(TR, 14)
SmoothedAtrRsi = WiMA(AtrRsi, 14)

// Fast ATR Trailing Levels
DeltaFastAtrRsi = SmoothedAtrRsi * Fast

var float longband = na
var float shortband = na
var int trend = na

newshortband = RSIndex + DeltaFastAtrRsi
newlongband = RSIndex - DeltaFastAtrRsi
longband := RSIndex[1] > longband and RSIndex > longband ? math.max(longband, newlongband) : newlongband
shortband := RSIndex[1] < shortband and RSIndex < shortband ? math.min(shortband, newshortband) : newshortband
trend := ta.cross(RSIndex, shortband[1]) ? 1 : ta.cross(longband[1], RSIndex) ? -1 : nz(trend[1], 1)
FastAtrRsiTL = trend == 1 ? longband : shortband

// Slow ATR Trailing Levels
DeltaSlowAtrRsi = SmoothedAtrRsi * Slow

var float longband1 = na
var float shortband1 = na
var int trend1 = na

newshortband1 = RSIndex + DeltaSlowAtrRsi
newlongband1 = RSIndex - DeltaSlowAtrRsi
longband1 := RSIndex[1] > longband1 and RSIndex > longband1 ? math.max(longband1, newlongband1) : newlongband1
shortband1 := RSIndex[1] < shortband1 and RSIndex < shortband1 ? math.min(shortband1, newshortband1) : newshortband1
trend1 := ta.cross(RSIndex, shortband1[1]) ? 1 : ta.cross(longband1[1], RSIndex) ? -1 : nz(trend1[1], 1)
SlowAtrRsiTL = trend1 == 1 ? longband1 : shortband1

// Plot the indicators
plot(RSIndex, title="RSIndex", color=color.yellow)
plot(FastAtrRsiTL, title="FastAtrRSI", color=color.green)
plot(SlowAtrRsiTL, title="SlowAtrRSI", color=color.red)
